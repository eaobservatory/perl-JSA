#!/usr/bin/env starperl

=head1 NAME

jsahpxobs - submit observations for HPX tile generation

=head1 SYNOPSIS

  jsahpxobs -ut 20080705

=head1 DESCRIPTION

Submits processing jobs to the JSA specifically to generate healpix
tiles for individual observations.

=head1 OPTIONS

=over 4

=item B<-debug>

Enable debug messages and do not actually submit a processing job. Also
disables emailing.

=item B<-filter>

Filter to select.

=item B<-help>

Print help information.

=item B<-man>

Print full manual.

=item B<-noemail>

By default a mail message is sent with the job details. If this option is given
the message will be printed to standard output instead.

=item B<-priority>

Used to override the default priority for all jobs. Must be an integer between
-1023 and 1024. The default will be 1.

=item B<-project>

Name of a project to be processed.  If the "-ut" option is also supplied then night mode
will be used but selecting observations from this project.

=item B<-queue>

Can be used to override the queue that should be used for the data processing
at CADC. Can be left unset. Useful values are "JAC_REPROC" for low priority
reprocessing.

=item B<-ut>

Specify the UT date to be processed.

=item B<-version>

Print version information.

=back

=cut


use JAC::Setup qw/ omp sybase /;

use strict;
use warnings;

use JSA::Submission qw/%JUNK_OBSIDSS obs_is_fts2_or_pol2_RECIPE
                       determine_resource_requirement
                       echo_messages find_observations log_message
                       prepare_archive_db send_log_email submit_jobs
                       write_log_file/;

use OMP::Info::Obs;
use OMP::Constants qw/ :obs /;

use Getopt::Long;
use Pod::Usage;
use File::Basename;

prepare_archive_db();

# Handle command-line options.
my ( $help, $man, $project, $ut, $version, $priority, $noemail, $debug,
     $queue, $filter );
my $status = GetOptions( "help"      => \$help,
                         "man"       => \$man,
                         "project=s" => \$project,
                         "ut=s"      => \$ut,
                         "priority=i"=> \$priority,
                         "version"   => \$version,
                         "debug"     => \$debug,
                         "noemail"   => \$noemail,
                         'filter=s'  => \$filter,
                         "queue=s"     => \$queue,
                       );

pod2usage( 1 ) if $help;
pod2usage( -exitstatus => 0, -verbose => 2 ) if $man;

if( $version ) {
  print "Submit a HPX observation job to the CADC GridEngine for JSA processing.\n";
  print " Source code revision: 0.01\n";
  exit;
}

echo_messages($noemail || $debug);

# All observations are submitted regardless of status except for JUNK.
# A standard recipe is used.
$project = uc($project) if defined $project;
my ($mode, $grp) = find_observations($ut, $project, $priority, 'jsahpxobs');

if( scalar( @{$grp->obs} ) == 0 ) {
  log_message( "No observations were retrieved from the database.\n");
} else {
  log_message( scalar( @{$grp->obs} ) . " observations were retrieved from the database.\n\n");

  # Group information hash for submit_jobs().
  my %groups;

  foreach my $obs ( $grp->obs ) {

    my $hdr = $obs->hdrhash;

    my $obstype = uc( $hdr->{'OBS_TYPE'} );
    my $instrume = uc( $hdr->{'INSTRUME'} );
    my $backend = uc( $hdr->{'BACKEND'} );
    my $survey = uc($hdr->{'SURVEY'} // '');

    # No reduction recipe at CADC yet for (FTS|POL)-2.
    if ( obs_is_fts2_or_pol2_RECIPE( $backend, $obs->drrecipe() ) ) {

      log_message( sprintf "Skipped observation with OBSID %s from FTS-2/POL-2 reduction %s\n",
                      $obs->obsid(),
                      'based on recipe value'
                  );
      next;
    }

    # Currently does not work with anything other than SCUBA-2
    if ($instrume ne 'SCUBA-2') {
      log_message( "Instrument $instrume currently not supported for HPX obs processing\n");
      next;
    }

    # We process SCIENCE and POINTING for SCUBA-2, and SCIENCE for
    # ACSIS and the DAS.
    unless (
                ( $instrume eq 'SCUBA-2' &&
                      ( $obstype eq 'SCIENCE' ||
                        $obstype eq 'POINTING' ) ) ||
                ( $backend eq 'ACSIS' && $obstype eq 'SCIENCE' ) ||
                ( $backend eq 'DAS'   && $obstype eq 'SCIENCE' )
           ) {
      next;
    }

    # Skip moving sources
    if (!defined $hdr->{"OBSRA"}) {
      log_message("Observation is from a moving source so skipping for HPX obs processing\n");
      next;
    }

    # Skip CLS for now.
    if ($survey eq 'CLS') {
      log_message("Observation is from CLS so skipping for HPX obs processing\n");
      next;
    }

    # Current default values for this observation in group handling and
    # recipe submission
    my %current;

    # if the data are truly JUNK we do not want to ask CADC to process the data at
    # all since the chances are excellent that the pipeline will fail
    if ( $obs->status == OMP__OBS_JUNK ) {
      log_message("Observation is junk and so will not be processed at all\n");
      next;
    }

    $current{'dprecipe'} = determine_resource_requirement($obs);

    # All observations are processed with the same recipe
    # which has hard-wired parameters. No special tweaking because
    # we know that it is a calibrator or a blank field
    $current{drparams} = "REDUCE_SCAN_JSA_PUBLIC";

    my @subsystems = $obs->subsystems;

    for my $subsys (@subsystems) {
      my @all_obsidss = $subsys->obsidss;
      my $obsidss = $all_obsidss[0];

      die "Error determining an OBSIDSS for an observation. Can not proceed\n"
        unless defined $obsidss;

      if (exists $JUNK_OBSIDSS{$obsidss}) {
	log_message( "Subsystem $obsidss will not be processed in any way\n");
	next;
      }

      my $subsyshdr = $subsys->hdrhash;

      if ((defined $filter) and ($filter ne $subsyshdr->{'FILTER'})) {
	log_message( "Subsystem $obsidss skipped due to filter selection\n");
	next;
      }

      # Get all the files, stripped of path
      my @files = map { basename($_) } $subsys->filename;

      # The grouping key is really OBSID+ASN_ID
      # where ASN_ID is the subsystem-specific grouping string
      my $ss_asn_id = (exists $subsyshdr->{ASN_ID} ? $subsyshdr->{ASN_ID}
                       : $subsyshdr->{FILTER} ); # SCUBA-2 hack until we get ASN_ID

      my $group = "hpx-" . $subsys->obsid . "-" . $ss_asn_id;

      push @{$groups{$group}{files}}, @files;
      $groups{$group}{mode} = "obs";
      $groups{$group}{dprecipe} = $current{dprecipe};
      for my $k (qw/ drparams recpars /) {
        $groups{$group}{$k} = $current{$k}
          if defined $current{drparams};
      }
    }
  }

  submit_jobs(\%groups, undef, $mode, $priority, $queue, $debug);
}

write_log_file('jsasubmit-hpx-scuba2', $ut, $project) unless $debug;

send_log_email('CADC HPX SCUBA-2 Obs Processing', $ut, $project)
    unless $noemail || $debug;

exit;

=head1 NOTES

All science/pointing observations are submitted unless they are marked junk.

No group handling is required as each observation is submitted as a standalone
job.

All files are assumed to be at CADC.

=head1 AUTHORS

Tim Jenness E<lt>t.jenness@jach.hawaii.eduE<gt>

=head1 COPYRIGHT

Copyright (C) 2014 Science and Technology Facilities Council.
All Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful,but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut
