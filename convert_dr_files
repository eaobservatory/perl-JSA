#!perl

=head1 NAME

convert_dr_files - Convert files created by ORAC-DR into CADC-compatible files.

=head1 SYNOPSIS

  convert_dr_files
  convert_dr_files *.sdf

=head1 DESCRIPTION

This program is used to convert files created by ORAC-DR into FITS
files that can be injested by the CADC.

=head1 ARGUMENTS

If no arguments are given the entire current directory is scanned for
compatible files. If file names are supplied on the command line only those
files will be considered.

=over 4

=item B<-debug>

Enable debug messages.

=item B<-help>

Print help information.

=item B<-list>

List what would and would not be converted without actually doing the conversion.

=item B<-man>

Print full manual.

=item B<-mode>

Specify the grouping mode. This does not change grouping behaviour; it
only changes collection IDs as stored by CADC. Can be one of 'night'
(for night grouping), 'project' (for project grouping) or 'public'. If
not given, then an error will be thrown.

=item B<-version>

Print version information.

=back

=head1 ENVIRONMENT

This program requires that the $STARLINK_DIR environment variable is
correctly defined.

All files are read from and written into the current working
directory.

=head1 AUTHORS

Brad Cavanagh E<lt>b.cavanagh@jach.hawaii.eduE<gt>
Tim Jenness E<lt>t.jenness@jach.hawaii.eduE<gt>

=head1 COPYRIGHT

Copyright (C) 2008, 2009 Science and Technology Facilities Council.
All Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful,but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

use strict;
use warnings;
use Getopt::Long;
use Pod::Usage;

use JAC::Setup qw/ omp oracdr sybase /;

use JSA::Convert qw/ convert_dr_files list_convert_plan /;
use JSA::Files qw/ scan_dir /;
use JSA::Headers qw/ read_headers /;

my ($help, $version, $man, $debug, $list, $mode);
my $status = GetOptions( "help" => \$help,
                         "version" => \$version,
                         "man" => \$man,
                         "list" => \$list,
                         "debug" => \$debug,
                         "mode=s" => \$mode,
                       );

pod2usage(1) if $help;
pod2usage(-exitstatus => 0, -verbose => 2) if $man;
if ($version) {
  print "convert_dr_files - Convert a directory of files to CADC JSA compliant files\n";
  exit;
}

if ($debug) {
  $JSA::Convert::DEBUG = 1;
  $JSA::Files::DEBUG = 1;
  $JSA::Starlink::DEBUG = 1;
}

if( ! defined( $mode ) ) {
  die "-mode parameter to convert_dr_files must be set";
}

my @files;
if (@ARGV) {
  @files = @ARGV;
} else {
  my %files = scan_dir( );
  @files = sort keys %files;
}

print "Opening ".@files." files\n" if $debug;
my %drhdrs = read_headers( @files );
if ($debug) {
  my @files = keys %drhdrs;
  print "Read ".@files." headers\n";
}
if ($list) {
  # List what would happen (assuming headers are okay)
  list_convert_plan( \%drhdrs );
} else {
  # Do the actual conversion
  convert_dr_files( \%drhdrs, { "mode" => $mode } );
}

exit;
