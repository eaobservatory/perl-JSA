#!/bin/env starperl

=head1 NAME

jsasubmit - Submit a job to the CADC GridEngine for JSA processing.

=head1 SYNOPSIS

  jsasubmit -ut 20080705

=head1 DESCRIPTION

Submits a processing job to the CADC data processing system.

=head1 OPTIONS

=over 4

=item B<-debug>

Enable debug messages and do not actually submit a processing job.

=item B<-help>

Print help information.

=item B<-man>

Print full manual.

=item B<-noemail>

By default a mail message is sent with the job details. If this option is given
the message will be printed to standard output instead.

=item B<-priority>

Used to override the default priority for all jobs. Must be an integer between
-1023 and 1024. The default will be 1.

=item B<-project>

Name of a project to be processed. Should not be used with the "-ut" option. Files
are not verified to be present at CADC in this mode.

=item B<-ut>

Specify the UT date to be processed. Should not be used with the "-project" option.
Files to be processed are verified to be at CADC before a job will be submitted.

=item B<-version>

Print version information.

=back

=cut


use JAC::Setup qw/ omp oracdr sybase /;

use strict;
use warnings;

use JSA::CADC_Copy;
use JSA::CADC_DP qw/ connect_to_cadcdp disconnect_from_cadcdp
                     create_recipe_instance /;
use JSA::Error qw/ :try /;
use JSA::Files qw/ file_to_uri /;
use JSA::Headers qw/ get_orac_instrument /;

use OMP::ArchiveDB;
use OMP::Info::Obs;
use OMP::Info::ObsGroup;
use ORAC::Inst::Defn qw/ orac_determine_inst_classes /;

use Getopt::Long;
use Net::SMTP;
use Pod::Usage;

my $TELESCOPE = 'JCMT';
my $BASEURL = "http://test.cadc-ccda.hia-iha.nrc-cnrc.gc.ca/dp/recipe/";
my $MAILHOST = 'mailhost.jach.hawaii.edu';
my $MAILTO = 'jcmtarch@jach.hawaii.edu';
my $MAILFROM = 'jcmtarch@jach.hawaii.edu';

# Array to hold messages that will be mailed out.
my @mailmessages;

# Don't fall back to files.
$OMP::ArchiveDB::FallbackToFiles = 0;

# Use DB for any date.
$OMP::ArchiveDB::AnyDate = 1;

# Fix search criteria to avoid being reset just before querying for data.
OMP::ArchiveDB->use_existing_criteria( 1 );

# Handle command-line options.
my ( $help, $man, $mode, $project, $ut, $version, $priority, $noemail, $debug );
my $status = GetOptions( "help"      => \$help,
                         "man"       => \$man,
                         "project=s" => \$project,
                         "ut=s"      => \$ut,
                         "priority=i"=> \$priority,
                         "version"   => \$version,
                         "debug"     => \$debug,
                         "noemail"   => \$noemail,
                       );

pod2usage( 1 ) if $help;
pod2usage( -exitstatus => 0, -verbose => 2 ) if $man;

if( $version ) {
  print "Submit a job to the CADC GridEngine for JSA processing.\n";
  print " Source code revision: 0.01\n";
  exit;
}

# Make sure the UT parameter is defined.
if( ! defined $ut && ! defined $project ) {
  die "Must include either -ut or -project parameter";
} elsif (defined $ut && defined $project ) {
  die "Can not include both -ut and -project parameters";
}

my $pristring = 'with default priority';
if (defined $priority) {
  $pristring = "with priority $priority";
}

if( defined( $project ) ) {
  $mode = "project";
  $project = uc( $project );
  push @mailmessages, "Running jsasubmit for project $project $pristring.\n";
} else {
  push @mailmessages, "Running jsasubmit for UT date $ut $pristring.\n";
  $mode = "night";
}

my $grp;
my $atCADC;
if( $mode eq 'project' ) {
  $grp = new OMP::Info::ObsGroup( projectid => $project,
                                  retainhdr => 1 );
} else {
  $grp = new OMP::Info::ObsGroup( telescope => $TELESCOPE,
                                  date => $ut,
                                  retainhdr => 1 );
  $atCADC = JSA::CADC_Copy::at_cadc( $ut );
}

if( scalar( @{$grp->obs} ) == 0 ) {
  push @mailmessages, "No observations were retrieved from the database.\n";
} else {
  push @mailmessages, scalar( @{$grp->obs} ) . " observations were retrieved from the database.\n\n";

  # Hash to hold list of groups.
  my %group;

  # Hash to hold list of ORAC::Frame::<inst> classes already loaded.
  my %frameclassloaded;

  foreach my $obs ( $grp->obs ) {

    my $hdr = $obs->hdrhash;

    my $obstype = uc( $hdr->{'OBS_TYPE'} );
    my $instrume = uc( $hdr->{'INSTRUME'} );
    my $backend = uc( $hdr->{'BACKEND'} );

    # We process SCIENCE and POINTING for SCUBA-2, and SCIENCE for
    # ACSIS.
    if( ! ( ( $instrume eq 'SCUBA-2' &&
              ( $obstype eq 'SCIENCE' ||
                $obstype eq 'POINTING' ) ) ||
            ( $backend eq 'ACSIS' && $obstype eq 'SCIENCE' ) ) ) {
      next;
    }

    # Check to see if we have OBSRA and OBSDEC. If they're defined,
    # the tracking system is J2000. Otherwise it's APP.
    if( ! defined( $hdr->{'TRACKSYS'} ) ) {
      if( defined( $hdr->{'OBSRA'} ) &&
          defined( $hdr->{'OBSDEC'} ) ) {
        $hdr->{'TRACKSYS'} = 'J2000';
      } else {
        $hdr->{'TRACKSYS'} = 'APP';
      }
    }

    if( defined( $hdr->{'OBSRA'} ) ) {
      $hdr->{'BASEC1'} = $hdr->{'OBSRA'};
    }
    if( defined( $hdr->{'OBSDEC'} ) ) {
      $hdr->{'BASEC2'} = $hdr->{'OBSDEC'};
    }

    if( defined( $hdr->{'FREQ_SIG_LOWER'} ) ) {
      $hdr->{'FRQSIGLO'} = $hdr->{'FREQ_SIG_LOWER'};
    }
    if( defined( $hdr->{'FREQ_SIG_UPPER'} ) ) {
      $hdr->{'FRQSIGHI'} = $hdr->{'FREQ_SIG_UPPER'};
    }

    if( ! defined( $hdr->{'SIMULATE'} ) ) {
      $hdr->{'SIMULATE'} = 1;
    }

    my $instrument = get_orac_instrument( $obs->fits );

    my ( $frameclass, $groupclass, $calclass, $instclass ) = orac_determine_inst_classes( $instrument );
    if( ! $frameclassloaded{$frameclass} ) {
      my $isok = eval " require $frameclass; 1; ";
      if( ! $isok ) {
        die "Could not load $frameclass: $@\n";
      }
      $frameclassloaded{$frameclass}++;
    }

    if( defined( $hdr->{'SUBHEADERS'} ) ) {
      foreach my $subhdr ( @{$hdr->{'SUBHEADERS'}} ) {
        my $tmphdr = $hdr;

        foreach my $key ( keys %$subhdr ) {
          $tmphdr->{$key} = $subhdr->{$key};
        }

        if( defined( $tmphdr->{'FREQ_SIG_LOWER'} ) ) {
          $tmphdr->{'FRQSIGLO'} = $tmphdr->{'FREQ_SIG_LOWER'};
        }
        if( defined( $tmphdr->{'FREQ_SIG_UPPER'} ) ) {
          $tmphdr->{'FRQSIGHI'} = $tmphdr->{'FREQ_SIG_UPPER'};
        }

        # Set ORAC_INSTRUMENT so SCUBA-2 works.
        my $ORAC_INSTRUMENT = '';
        if( defined( $tmphdr->{'SUBSYSNR'} ) &&
            $instrument eq 'SCUBA-2' ) {
          $ORAC_INSTRUMENT = 'SCUBA2_' . $tmphdr->{'SUBSYSNR'};
        }
        $ENV{'ORAC_INSTRUMENT'} = $ORAC_INSTRUMENT;

        my $frm = new $frameclass;
        $frm->hdr( %{$tmphdr} );
        my $group = $frm->findgroup;
        push @{$group{$group}}, $tmphdr->{'FILE_ID'};
      }
    } else {

      my $frm = new $frameclass;
      $frm->hdr( %{$hdr} );
      my $group = $frm->findgroup;
      push @{$group{$group}}, $obs->filename;
    }
  }

  push @mailmessages, "Connecting to CADC data processing database...";
  my $dbh = connect_to_cadcdp;
  push @mailmessages, "connected!\n\n";

  foreach my $group ( sort keys %group ) {
    push @mailmessages, "Requesting CADC processing of the following files:\n";
    push @mailmessages, map { "$_\n" } @{$group{$group}};
    my @members = map { file_to_uri( $_ ) } @{$group{$group}};

    # Only check for night mode. If we're in project mode, assume the
    # files are at CADC.
    if( $mode eq 'night' ) {
      push @mailmessages, "Checking to ensure files are at CADC...\n";
      my $there = 1;
      foreach my $file ( @{$group{$group}} ) {
        $file =~ s/\.sdf$//;
        if( ! exists( $atCADC->{$file} ) ) {
          $there = 0;
          push @mailmessages, "$file is not at CADC!\n";
        }
      }
      if( ! $there ) {
        push @mailmessages, "One or more files from current group not at CADC.\nSkipping to next group.\n";
        next;
      }
      push @mailmessages, "All files are at CADC. Submitting processing request.\n";
    }

    # Submit the job.
    my $recipe_id;
    try {
      my %opts;
      $opts{'mode'} = $mode;
      if( $mode eq 'project' ) {
        $opts{'project'} = $project;
      }
      $opts{priority} = $priority if defined $priority;

      if ($debug) {
        push(@mailmessages, "Would be submitting job with options:\n");
        for my $k (sort keys %opts) {
           push(@mailmessage, "\t$k : ".(defined $opts{$k} ? $opts{$k} : "<undef>")."\n");
        }
      } else {
        $recipe_id = create_recipe_instance( $dbh, \@members, \%opts );
      }
    }
    catch JSA::Error::CADCDB with {
      my $Error = shift;
      push @mailmessages, "Error in CADC DB connectivity: $Error";
      push @mailmessages, "Skipping to next group.\n";
    }
    otherwise {
      my $Error = shift;
      $Error->throw;
    };
    exit if ! defined $recipe_id;
    push @mailmessages, "Request submitted with recipe instance $recipe_id.\n";
    push @mailmessages, "Recipe URL: $BASEURL" . hex($recipe_id) . "\n";
    push @mailmessages, "\n";
  }

  push @mailmessages, "Disconnecting from CADC data processing database...";
  disconnect_from_cadcdp( $dbh );
  push @mailmessages, "disconnected!\n\n";
  push @mailmessages, "Data processing requests complete.\n";

}

if ($noemail || $debug) {
  print @mailmessages;
} else {
  # Send the email.
  my $smtp = Net::SMTP->new( $MAILHOST );
  $smtp->mail( $MAILFROM );
  $smtp->to( $MAILTO );

  $smtp->data();
  $smtp->datasend( "To: $MAILTO\n" );
  $smtp->datasend("Subject: CADC Processing for " . ( defined( $ut ) ? $ut : $project ) . "\n");
  $smtp->datasend("\n");
  $smtp->datasend( @mailmessages );

  $smtp->quit;
}


=head1 AUTHORS

Brad Cavanagh E<lt>b.cavanagh@jach.hawaii.eduE<gt>
Tim Jenness E<lt>t.jenness@jach.hawaii.eduE<gt>

=head1 COPYRIGHT

Copyright (C) 2009-2010 Science and Technology Facilities Council.
All Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful,but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut
